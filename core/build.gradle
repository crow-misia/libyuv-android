apply plugin: "com.android.library"
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group maven.groupId
version maven.version

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }

    libraryVariants.all {
        it.generateBuildConfigProvider.configure {
            it.enabled = false
        }
    }

    buildTypes {
        debug {
            debuggable false
            jniDebuggable false
        }
        release {
            debuggable false
            jniDebuggable false
        }
    }

    externalNativeBuild {
        ndkBuild {
            setPath(new File(projectDir, "src/main/jni/Android.mk"))
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
}

dependencies {
    api deps.annotations
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name maven.name
            packaging 'aar'
            groupId maven.groupId
            artifactId maven.artifactId
            version maven.version
            url maven.siteUrl
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_apikey') ? bintray_apikey : ''

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'maven'
        name = maven.name
        desc = maven.desc
        licenses = maven.licenses
        websiteUrl = maven.siteUrl
        vcsUrl = maven.gitUrl
        issueTrackerUrl = maven.issueTrackerUrl
        githubRepo = maven.githubRepo
        labels = maven.labels
        publicDownloadNumbers = true
        version {
            name = maven.version
            released = new Date()
            gpg {
                sign = true
                passphrase = project.hasProperty('bintray_gpg_password') ? bintray_gpg_password : ''
            }
        }
    }
}
